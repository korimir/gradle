/*
 * Copyright 2014 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

package org.gradle.play.plugins
import org.gradle.api.JavaVersion
import org.gradle.integtests.fixtures.AbstractIntegrationSpec
import org.gradle.integtests.fixtures.TestResources
import org.gradle.test.fixtures.archive.JarTestFixture
import org.gradle.util.AvailablePortFinder
import org.gradle.util.TextUtil
import org.junit.Rule
import spock.lang.Timeout

class PlayApplicationPluginIntegrationTest extends AbstractIntegrationSpec {

    @Rule
    public final TestResources resources = new TestResources(temporaryFolder)

    def setup() {
        buildFile << """
        plugins {
            id 'play-application'
        }

        model {
            components {
                myApp(PlayApplicationSpec)
            }
        }

        tasks.withType(ScalaCompile) {
            scalaCompileOptions.fork = true
            scalaCompileOptions.useAnt = false
            scalaCompileOptions.forkOptions.memoryMaximumSize = '1g'
            scalaCompileOptions.forkOptions.jvmArgs = ['-XX:MaxPermSize=512m']
        }

        tasks.withType(TwirlCompile){
            fork = true
            forkOptions.memoryInitialSize =  "256m"
            forkOptions.memoryMaximumSize =  "512m"
        }
"""
    }

    def "can register PlayApplicationSpec component"() {
        def javaVersion = JavaVersion.current();
        when:
        succeeds "components"
        then:
        output.contains(TextUtil.toPlatformLineSeparators("""
DefaultPlayApplicationSpec 'myApp'
----------------------------------

Source sets
    No source sets.

Binaries
    DefaultPlayApplicationBinarySpec 'myAppBinary'
        build using task: :myAppBinary
        platform: java${javaVersion.majorVersion}
        tool chain: Play Framework 2.10-2.3.5 (JDK ${javaVersion.majorVersion} (${javaVersion.toString()})"""))
    }

    def "builds play binary"() {
        when:
        succeeds("assemble")
        then:
        executed(":createMyAppBinaryJar", ":myAppBinary", ":assemble")
        skipped(":routesCompileMyAppBinary" , ":twirlCompileMyAppBinary", ":scalaCompileMyAppBinary")
        and:
        file("build/jars/myApp/myAppBinary.jar").exists()
    }


    def "can build play apps generated by 'play new'"() {
        given:
        resources.maybeCopy("PlayApplicationPluginIntegrationTest/playNew")
        buildFile << """
        repositories{
            jcenter()
            maven{
                name = "typesafe-maven-release"
                url = "http://repo.typesafe.com/typesafe/maven-releases"
            }
        }
"""
        when:
        succeeds("assemble")
        then:
        executed(":routesCompileMyAppBinary", ":twirlCompileMyAppBinary", ":createMyAppBinaryJar", ":myAppBinary", ":assemble")
        def jarTestFixture = new JarTestFixture(file("build/jars/myApp/myAppBinary.jar"))
        jarTestFixture.assertContainsFile("Routes.class")
        jarTestFixture.assertContainsFile("views/html/index.class")
        jarTestFixture.assertContainsFile("views/html/main.class")
        jarTestFixture.assertContainsFile("controllers/Application.class")
        jarTestFixture.assertContainsFile("images/favicon.png")
        jarTestFixture.assertContainsFile("stylesheets/main.css")
        jarTestFixture.assertContainsFile("javascripts/hello.js")
        jarTestFixture.assertContainsFile("application.conf")

        when:
        succeeds("createMyAppBinaryJar")
        then:
        skipped(":createMyAppBinaryJar", ":twirlCompileMyAppBinary")
    }

    /**
     * This tests the right behaviour of the "run" task provided by the play plugin.
     * For further testing of correct app behaviour we should setup a more manageable
     * test fixture based on play.core.server.ServerProcess
     * */
    @Timeout(60)
    def "can run play app generated by 'play new'"() {
        setup:
        def httpPort = AvailablePortFinder.createPrivate().nextAvailable
        when:
        resources.maybeCopy("PlayApplicationPluginIntegrationTest/playNew")
        buildFile << """
        repositories{
            jcenter()
            maven{
                name = "typesafe-maven-release"
                url = "http://repo.typesafe.com/typesafe/maven-releases"
            }
        }

        model {
            tasks.runMyAppBinary {
                ignoreExitValue = true
                String content;
                systemProperty("http.port", "$httpPort")
                doFirst{
                    Thread.start{
                        available("http://localhost:$httpPort")
                        content = new URL("http://localhost:$httpPort").text
                        String pid = file('RUNNING_PID').text
                        project.exec{
                            if(org.gradle.internal.os.OperatingSystem.current().isWindows()){
                                executable "taskkill"
                                args "/F", "/PID", pid
                            }else{
                                executable "kill"
                                args "-9", pid
                            }
                            ignoreExitValue = true
                        }
                    }
                }
                doLast{
                    file('RUNNING_PID').delete()
                    assert content.contains("Your new application is ready.")
                }
            }
        }

        def available(String theUrl, int timeout = 30000) {
            URL url = new URL(theUrl)
            long expiry = System.currentTimeMillis() + timeout
            while (System.currentTimeMillis() <= expiry) {
                try {
                    url.text
                    return
                } catch (IOException e) {
                    // continue
                }
                Thread.sleep(200)
            }
            throw new RuntimeException("Timeout waiting for \$theUrl to become available.");
        }
"""

        then:
        succeeds("runMyAppBinary")
    }
}